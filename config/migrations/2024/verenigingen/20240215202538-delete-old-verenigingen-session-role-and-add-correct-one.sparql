PREFIX ext:     <http://mu.semte.ch/vocabularies/ext/>
PREFIX skos:    <http://www.w3.org/2004/02/skos/core#>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX foaf:    <http://xmlns.com/foaf/0.1/>

#
# Delete old verenigingen app session role and insert a new
# one only for Gemeenten.
#

DELETE {
  GRAPH <http://mu.semte.ch/graphs/public> {
    ?account ext:sessionRole "abb_loketverenigingenapp" .
  }
}
INSERT {
  GRAPH <http://mu.semte.ch/graphs/public> {
    ?account ext:sessionRole "LoketLB-verenigingenGebruiker" .
  }
}
WHERE {
  ?bestuurseenheid a besluit:Bestuurseenheid ;
    besluit:classificatie <http://data.vlaanderen.be/id/concept/BestuurseenheidClassificatieCode/5ab0e9b8a3b2ca7c5e000001> . # Gemeente

  ?persoon a foaf:Person ;
    foaf:member ?bestuurseenheid ;
    foaf:account ?account .

  ?account a foaf:OnlineAccount ;
    foaf:accountServiceHomepage <https://github.com/lblod/mock-login-service> .
}
